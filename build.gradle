buildscript {
	ext {
		springBootVersion = '1.4.0.RELEASE'
	}
	repositories {
		mavenCentral()
		jcenter()
		maven { url "https://plugins.gradle.org/m2/" }		
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.franzbecker:gradle-lombok:1.7")
        classpath("org.standardout:gradle-versioneye-plugin:1.4.0")
        classpath("de.undercouch:gradle-download-task:3.1.1")               
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: 'jacoco'
apply plugin: 'io.franzbecker.gradle-lombok'
apply plugin: 'org.standardout.versioneye'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'de.undercouch.download'

group = 'com.microservicesteam'
version = '0.0.1-SNAPSHOT'

jar {
	baseName = 'nutaxi-cache'
	version = "${version}"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

repositories {
	mavenCentral()
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter')
	compile("org.springframework.boot:spring-boot-starter-web")
	compile('org.springframework.boot:spring-boot-starter-redis')
	compile('org.apache.commons:commons-lang3:3.4')
	compile('org.projectlombok:lombok:1.16.10')
	compile('com.google.guava:guava:19.0')
	
	testCompile('org.assertj:assertj-core:3.5.2')	
	testCompile('com.orange.redis-embedded:embedded-redis:0.6')		
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('com.jayway.jsonpath:json-path')
}


eclipse {
	classpath {
		 containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
		 containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
	}
}

lombok {
    version =  "1.16.10"
}

jacoco {
    toolVersion = "0.7.7.201606060606"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task checkstyleConfigDownload(type: de.undercouch.gradle.tasks.download.Download) {
    src 'https://raw.githubusercontent.com/microservicesteam/nutaxi-cache/master/config/checkstyle/checkstyle.xml'
    dest buildDir
}

checkstyle {
    toolVersion = "7.1"
    configFile = new File(buildDir, "checkstyle.xml")
}

pmd {
	toolVersion = "5.5.1"
}

findbugs {
	toolVersion = "3.0.1"
}

check.dependsOn jacocoTestReport
checkstyleMain.dependsOn checkstyleConfigDownload
checkstyleTest.dependsOn checkstyleConfigDownload